import org.gradle.api.internal.file.BaseDirFileResolver
import org.gradle.api.internal.file.FileResolver
import org.gradle.internal.nativeplatform.filesystem.FileSystems

buildscript {
	repositories {
    	mavenCentral()
    }
	dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '0.6.4'
    }
}

apply plugin: 'gradlefx'
apply plugin: 'scaffold'

type = 'swf'
frameworkLinkage = 'none'
mainClass = 'Main.as'

flexUnit.command = resolveFlexUnitCommand(System.getenv()['FLASH_PLAYER_EXE'])
def flexUnitHome = resolveFlexUnitHome(System.getenv()['FLEXUNIT_HOME'])

def flaDirectory = 'src/main/fla'
def scriptsDirectory = 'src/main/scripts' 
def flaBuildScript = 'publish.jsfl'

def apparatDirectory = resolveApparatHome(System.getenv()['APPARAT_HOME'])

repositories {
    add(new org.apache.ivy.plugins.resolver.URLResolver()) {
        name = 'Apache'
        addArtifactPattern 'http://apache.cu.be/flex/4.9.0/binaries/apache-flex-sdk-4.9.0-bin.zip'
    } 
}

dependencies {
    flexSDK 'org.apache:apache-flex-sdk:4.9.0@zip'

    test files("${flexUnitHome}/flexunit-4.1.0-8-flex_4.1.0.16076.swc",
            "${flexUnitHome}/flexUnitTasks-4.1.0-8.jar",
            "${flexUnitHome}/flexunit-cilistener-4.1.0-8-4.1.0.16076.swc")
         
    merged files("${libsDir}")    
}

sdkAutoInstall {
    showPrompts = false
}

task flaBuild << { 
    copy {
    	from "${scriptsDirectory}/${flaBuildScript}"
        into buildDir
   	}                                               
   	
   	File finalFlaBuildScript = file("${buildDir}/${flaBuildScript}")     
   	File flaDirectoryPath = file(flaDirectory)     
   	String publishCall=/publishFlasWin("${flaDirectoryPath}\", "${libsDir}\", "${buildDir}\reports\");/.replace('\\', '\\\\')         
   	finalFlaBuildScript.append(publishCall)    
   	   	  	                                                                                               
   	ant.exec(executable:"cmd"){
   		arg(value:"/c")
   		arg(value:/"${finalFlaBuildScript}"/)
   	}
} 

compileFlex.dependsOn flaBuild

task compress << {
	println apparatDirectory
}

String resolveFlexUnitCommand(String flexUnitCommand){
	if (flexUnitCommand == null) {
    	flexUnitCommand = resolveFile('debugPlayer', 'flashplayer_11_sa.exe',
            'http://download.macromedia.com/pub/flashplayer/updaters/11/flashplayer_11_sa.exe')            
	}                                                                                               
	
	return flexUnitCommand
}

String resolveFlexUnitHome(String flexUnitHome){
	if (flexUnitHome == null) {   
		flexUnitHome = resolveZip('flexunit', 'flexunit-4.1.0-8-4.1.0.16076.zip', 
			'http://flexunit.org/releases/flexunit-4.1.0-8-4.1.0.16076.zip')  
	}     
	
	return flexUnitHome
}

String resolveApparatHome(String apparatHome){
	if(apparatHome == null){
		apparatHome = resolveZip('apparat-1.0-SNAPSHOT', 'apparat-1.0-SNAPSHOT-bin.zip', 
			'http://apparat.googlecode.com/files/apparat-1.0-SNAPSHOT-bin.zip')  
	}
	return apparatHome
}

File resolveFile(String fileDirName, String fileName, String downloadUrl) {
    println("Resolve file: ${fileName}")

    FileResolver gradleUserHomeDirectoryResolver = new BaseDirFileResolver(FileSystems.default, gradleFxUserHomeDir)
    File fileDirectoryLocation = gradleUserHomeDirectoryResolver.resolve(fileDirName)

    FileResolver fileLocationResolver = new BaseDirFileResolver(FileSystems.default, fileDirectoryLocation)
    File fileLocation = fileLocationResolver.resolve(fileName)

    if (!fileLocation.exists()) {
        println("Download file from url: ${downloadUrl}")

        fileDirectoryLocation.mkdirs()
        fileLocation.createNewFile()
        fileLocation.withOutputStream { out -> out << new URL(downloadUrl).openStream() }
    }

    return fileLocation
}   

File resolveZip(String dirName, String zipName, String downloadUrl){
	println("Resolve zip: ${zipName}")

    FileResolver gradleUserHomeDirectoryResolver = new BaseDirFileResolver(FileSystems.default, gradleFxUserHomeDir)
    File directoryLocation = gradleUserHomeDirectoryResolver.resolve(dirName)

    FileResolver zipLocationResolver = new BaseDirFileResolver(FileSystems.default, directoryLocation)
    File zipFile = zipLocationResolver.resolve(zipName)

    if (!zipFile.exists()) {
       	File downloadZip = resolveFile(dirName, zipName, downloadUrl)

        println("unzip file: ${zipName}")

       	ant.unzip(src: downloadZip, dest: gradleFxUserHomeDir, overwrite: "true")
	}

    return directoryLocation; 
}    

